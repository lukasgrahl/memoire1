###########################################################################
# Real Business Cycle model
###########################################################################

tryreduce
{
    U[], Tc[];
};



block HOUSEHOLD
{
    definitions
    {
        u[] = (c[] ^ (1 - sigma_C) / (1 - sigma_C) - n[] ^ (1 + sigma_L) / (1 + sigma_L));
    };

    controls
    {
        c[], n[], I[], k[];
    };

    objective
    {
        U[] = u[] + beta * E[][U[1]];
    };

    constraints
    {
        c[] + I[] = r[] * k[-1] + w[] * n[] : lambda[];
        k[] = (1 - delta) * k[-1] + I[];
    };

    calibration
    {
        beta  = 0.99;
        delta = 0.02;

        sigma_C = 1.5;
        sigma_L = 2.0;
    };
};

block FIRM
{
    controls
    {
        k[-1], n[];
    };

    objective
    {
        Tc[] = -(r[] * k[-1] + w[] * n[]);
    };

    constraints
    {
        y[] = a[] * k[-1] ^ alpha * n[] ^ (1 - alpha) : mc[];
    };

    identities
    {
        # Perfect competition
        mc[] = 1;
    };

    calibration
    {
        alpha = 0.35;
    };
};

block TECHNOLOGY_SHOCKS
{
    identities
    {
        log(a[]) = rho_A * log(a[-1]) + epsilon_a[];
    };

    shocks
    {
        epsilon_a[] ;
    };

    calibration
    {
        rho_A = 0.95;
        sigma_epsilon_A = 0.05;
    };
};