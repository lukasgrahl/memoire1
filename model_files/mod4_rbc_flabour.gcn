###########################################################################
# RBC fixed labour supply
# Source:  Campbell - Investigating the mechanism
# Author: Lukas A. Grahl
###########################################################################


tryreduce
{
    U[], TC[];
};



block HOUSEHOLD
{
    definitions
    {
        u[] = (C[] ^ (1 - sigma_C)) / (1 - sigma_C) ;
    };

    controls
    {
        C[], K[];
    };

    objective
    {
        U[] = u[] + beta * E[][U[1]];
    };

    constraints
    {
#        C[] + I[] = r[] * K[-1] + w[] : lambda[];
#        K[] = (1 - delta) * K[-1] + I[];

        K[] = (1 - delta) * K[-1] + Y[] - C[] : lambda[];
    };

    calibration
    {
        beta  = 0.99;
        delta = 0.02;

        sigma_C = 1.5;
    };
};

block FIRM
{
    controls
    {
        K[-1] ;
    };

    objective
    {
        TC[] = -(r[] * K[-1] + w[]) ;
    };

    constraints
    {
        Y[] = A[] ^ alpha * K[-1] ^ (1 - alpha) : mc[];
    };

    identities
    {
        # Perfect competition
        mc[] = 1;
        # no flexible labour supply
        w[] = 1; 
        R[] = (1 - alpha) * (A[] / K[]) ^ alpha + (1 - delta) ;
    };

    calibration
    {
        alpha  = 0.35;
    };
};

block TECHNOLOGY_SHOCKS
{
    identities
    {
        log(A[]) = rho_A * log(A[-1]) + epsilon_A[];
    };

    shocks
    {
        epsilon_A[] ~ N(mean=0, sd=sigma_epsilon_A);
    };

    calibration
    {
        rho_A ~ Beta(mean=0.95, sd=0.04) = 0.95;
        sigma_epsilon_A ~ Gamma(alpha=2, beta=0.1) = 0.05;
    };
};